using <string.h>::{ memcpy, memset }
using <stdlib.h> as stdlib

fntype FreeFunction(void mut * mut ptr) -> void;
fntype AllocFunction(usize size) -> void mut *;

static FreeFunction mut free_function = (FreeFunction) stdlib::free;
static AllocFunction mut alloc_function = (AllocFunction) stdlib::malloc;

export fn copy(void mut * mut dst, void *src, usize size) -> void *
  model safe(dst)
  model safe(src)
  model safe(return)
{
  static_attest(safe(dst));
  static_attest(safe(src));
  dst = memcpy(dst, src, size);
  static_attest(safe(dst));
  return dst;
}

export fn zero(void mut * mut dst, usize length) -> void *
  model safe(dst)
  model safe(return)
{
  static_attest(safe(dst));
  dst = memset(dst, 0, length);
  static_attest(safe(dst));
  return dst;
}

export fn alloc(usize length) -> void *
  model safe(return)
{
  static_attest(safe(alloc_function));
  void mut *ptr = alloc_function(length);
  static_attest(safe(ptr));
  zero(ptr, length);
  static_attest(safe(ptr));
  return ptr;
}

export fn free(void mut *ptr)
  model safe(ptr)
{
  static_attest(safe(ptr));
  static_attest(safe(free_function));
  free_function(ptr);
}

export fn duplicate(void *src, usize size) -> void * {
  return copy((void mut *) alloc(size), src, size);
}

export fn nonzero(void mut *ptr) -> bool
{
  return 0 != ptr;
}

export fn set_alloc(AllocFunction new_alloc_function) {
  alloc_function = new_alloc_function;
}

export fn set_free(FreeFunction new_free_function) {
  free_function = new_free_function;
}
